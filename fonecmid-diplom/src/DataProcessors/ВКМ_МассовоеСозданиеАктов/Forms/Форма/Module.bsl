#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьРТУ(Команда)
	
	НачалоПериода = Объект.Период.ДатаНачала;
	КонецПериода = Объект.Период.ДатаОкончания;
	
	ТекстСообщения = "";
	
	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		
		ТекстСообщения = "Для создания РТУ необходимо выбрать период"; 
		
	ИначеЕсли Месяц(НачалоПериода) <> Месяц(КонецПериода) 
		Или НачалоПериода <> НачалоМесяца(НачалоПериода) 
		Или КонецПериода <> КонецМесяца(КонецПериода) Тогда 
		
		ТекстСообщения = "Для корректного создания РТУ необходимо установить период в 1 месяц";
		
	КонецЕсли;
	
	Если ТекстСообщения <> "" Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Поле = "Объект.Период";
		Сообщение.УстановитьДанные(Объект);
		Сообщение.Сообщить();  
		Возврат;
		
	КонецЕсли;	
	
	ДлительнаяОперация = СоздатьРТУНаСервере();

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Интервал = 1;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	
	ОповещениеОПрогрессе = Новый ОписаниеОповещения("ОповещениеОПрогрессе", ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
    ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессе;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОповещениеОЗавершении", ЭтотОбъект);

	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания); 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоздатьРТУНаСервере() 
		
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор,
		"Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьРТУ", Объект.Период.ДатаНачала,
		Объект.Период.ДатаОкончания);

	Возврат ДлительнаяОперация; 

КонецФункции

&НаКлиенте
Процедура ОповещениеОПрогрессе(Прогресс, ДополнительныеПараметры) Экспорт

	Если Прогресс.Прогресс <> Неопределено Тогда
		
		Состояние("Создаются РТУ по договорам на абонентсткое обслуживание", Прогресс.Прогресс.Процент);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Статус = "Выполнено" Тогда
		
		ОписаниеРезультата = "выполнено!";
		РезультатВыполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		Для Каждого Результат Из РезультатВыполнения Цикл
			
			Если Результат.РТУЗаписана Тогда
				НоваяСтрока = Объект.Договоры.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат);
			КонецЕсли;
			
			Если Результат.Свойство("ОписаниеОшибки") Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = Результат.ОписаниеОшибки;
				Сообщение.Сообщить(); 
			КонецЕсли; 
		КонецЦикла;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ОписаниеОшибки = Результат.КраткоеПредставлениеОшибки;
		ОписаниеРезультата = СтрШаблон("ошибка. %1", ОписаниеОшибки);
	Иначе
		ОписаниеРезультата = Результат.Статус; 
	КонецЕсли;
	
	ТекстСообщения = СтрШаблон("Результат выполнения операции: %1", ОписаниеРезультата);
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();		
		
КонецПроцедуры

#КонецОбласти

