#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьРТУ(Команда)
	
	ДлительнаяОперация = СоздатьРТУНаСервере(УникальныйИдентификатор, Объект.Период.ДатаНачала,
		Объект.Период.ДатаОкончания);

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Интервал = 1;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

	ОповещениеОПрогрессе = Новый ОписаниеОповещения("ОповещениеОПрогрессе", ЭтотОбъект);

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОповещениеОЗавершении", ЭтотОбъект);

	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания); 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СоздатьРТУНаСервере(УникальныйИдентификатор, НачалоПериода, КонецПериода)
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор,
		"Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьРТУ", НачалоПериода, КонецПериода);

	Возврат ДлительнаяОперация; 

КонецФункции

&НаКлиенте
Процедура ОповещениеОПрогрессе(Прогресс, ДополнительныеПараметры) Экспорт

	Если Прогресс.Прогресс <> Неопределено Тогда
		
		Состояние("Создаются РТУ по договорам на абонентсткое обслуживание", Прогресс.Прогресс.Процент);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Статус = "Выполнено" Тогда
		РезультатВыполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ОписаниеОшибки = Результат.КраткоеПредставлениеОшибки;
		РезультатВыполнения = СтрШаблон("ошибка. %1", ОписаниеОшибки);
	Иначе
		РезультатВыполнения = Результат.Статус; 
	КонецЕсли;
	
	ТекстСообщения = СтрШаблон("Результат выполнения операции: %1", РезультатВыполнения);
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();		
		
КонецПроцедуры

#КонецОбласти

