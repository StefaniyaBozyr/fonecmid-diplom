#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция СоздатьРТУ(НачалоПериода, КонецПериода) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Организация КАК Организация,
		|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	РеализацияТоваровУслуг.Ссылка КАК РТУ,
		|	&КонецПериода КАК Дата
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ПО ДоговорыКонтрагентов.Ссылка = РеализацияТоваровУслуг.Договор
		|		И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И НЕ РеализацияТоваровУслуг.ПометкаУдаления
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
		|	И ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействияДоговора < &КонецПериода
		|	И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействияДоговора > &НачалоПериода
		|	И РеализацияТоваровУслуг.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	ТаблицаДоговоров = Запрос.Выполнить().Выгрузить();
	
	КоличествоСоздаваемыхРТУ = ТаблицаДоговоров.Количество();
	
	Счетчик = 0;  
	
	МассивРезультата = Новый Массив;
		
	Для Каждого Договор Из ТаблицаДоговоров Цикл
		
		СтруктураРезультата = Новый Структура;  
		СтруктураРезультата.Вставить("Договор", Договор.Договор);
		
		ДокументРТУ = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		ДокументРТУ.Заполнить(Договор); 
		
		ДокументРТУ.ВКМ_ВыполнитьАвтозаполнение();  
		
		ДокументРТУ.СуммаДокумента = ДокументРТУ.Товары.Итог("Сумма") + ДокументРТУ.Услуги.Итог("Сумма");
 				
		РеквизитыЗаполнены = ДокументРТУ.ПроверитьЗаполнение();
		
		Если РеквизитыЗаполнены Тогда
			
			//++ Бозырь С.Р. 23.08.2025 Дипломная работа - исправление (убрала запись РТУ до проверки на заполненность реквизитов)
			Попытка
				
				ДокументРТУ.Записать(РежимЗаписиДокумента.Проведение);
				СтруктураРезультата.Вставить("РТУ", ДокументРТУ.Ссылка);
				СтруктураРезультата.Вставить("Проведен", Истина);
				
			Исключение 
				
				СтруктураРезультата.Вставить("ОписаниеОшибки", СтрШаблон("Не удалось провести РТУ по договору %1 - %2", Договор.Договор, ОписаниеОшибки()));
				
			КонецПопытки; 
			//-- Бозырь С.Р.
			
		Иначе
			
			СтруктураРезультата.Вставить("ОписаниеОшибки", СтрШаблон("Не удалось провести РТУ по договору %1 - не были заполнены обязательные поля", Договор.Договор));
			
		КонецЕсли;
						
		МассивРезультата.Добавить(СтруктураРезультата);
		
		Счетчик = Счетчик + 1;
		ПроцентПрогресса = Счетчик * 100 / КоличествоСоздаваемыхРТУ;
		ДлительныеОперации.СообщитьПрогресс(ПроцентПрогресса);
							 
	КонецЦикла;
	
	Возврат МассивРезультата;
	
КонецФункции

#КонецОбласти

#КонецЕсли