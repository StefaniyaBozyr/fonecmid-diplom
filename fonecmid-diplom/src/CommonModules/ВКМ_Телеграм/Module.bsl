#Область СлужебныеПроцедурыИФункции

Процедура ВКМ_ОтправкаУведомленийВТелеграм() Экспорт
	
	ОбработатьУведамленияТелеграмБоту();

КонецПроцедуры

Процедура СоздатьУведомлениеТелеграмБоту(ТекстСообщения) Экспорт
	
	НовыйЭлемент = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
	НовыйЭлемент.ТекстСообщения = ТекстСообщения;
	НовыйЭлемент.Записать();
	
КонецПроцедуры

Процедура ОбработатьУведамленияТелеграмБоту()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения,
		|	ВКМ_УведомленияТелеграмБоту.Ссылка
		|ИЗ
		|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		РезультатОтправки = ОтправитьСообщениеВТелеграм(ВыборкаДетальныеЗаписи.ТекстСообщения);
		
		Если РезультатОтправки.СообщениеОтправлено Тогда
			
			УведомлениеОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			УведомлениеОбъект.Удалить();
			
		Иначе
			
			ТекстОшибки = СтрШаблон("Не удалось отправить сообщение в телеграм: %1", РезультатОтправки.ТекстОшибки);	
			ЗаписьЖурналаРегистрации("ВКМ_Телеграм.ОбработатьУведамленияТелеграмБоту", УровеньЖурналаРегистрации.Информация, , , ТекстОшибки); 		
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОтправитьСообщениеВТелеграм(ТекстСообщения)
	
	РезультатОтправки = Новый Структура;
	РезультатОтправки.Вставить("СообщениеОтправлено", Ложь);
	РезультатОтправки.Вставить("ТекстОшибки", "");

	ТокенТелеграм = Константы.ВКМ_ТокенБотаТелеграм.Получить();
	ИдентификаторГруппыТелеграм = Константы.ВКМ_ИдентификаторГруппыТелеграм.Получить();

	Если Не ЗначениеЗаполнено(ТокенТелеграм) Или Не ЗначениеЗаполнено(ИдентификаторГруппыТелеграм) Тогда

		РезультатОтправки.СообщениеОтправлено = Ложь;
		РезультатОтправки.ТекстОшибки = "Токен бота или идентификатор группы Телеграм не заполнены";
		
		Возврат РезультатОтправки;
		
	КонецЕсли;

	Попытка

		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
		Соединение = Новый HTTPСоединение("api.telegram.org", 443, , , , , ЗащищенноеСоединение);

		АдресРесурса = СтрШаблон("bot%1/sendMessage", ТокенТелеграм);
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type", "application/json");

		Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);

		СтруктураСообщения = Новый Структура;
		СтруктураСообщения.Вставить("chat_id", ИдентификаторГруппыТелеграм);
		СтруктураСообщения.Вставить("text", ТекстСообщения);

		ЗаписьJSON = Новый ЗаписьJSON; 
		ЗаписьJSON.УстановитьСтроку();
		ЗаписатьJSON(ЗаписьJSON, СтруктураСообщения);
		СтрокаJSON = ЗаписьJSON.Закрыть();

		Запрос.УстановитьТелоИзСтроки(СтрокаJSON);

		Ответ = Соединение.ОтправитьДляОбработки(Запрос);

		Если Ответ.КодСостояния = 200 Тогда

			РезультатОтправки.СообщениеОтправлено = Истина;

		Иначе

			РезультатОтправки.СообщениеОтправлено = Ложь;
			РезультатОтправки.ТекстОшибки = Ответ.ПолучитьТелоКакСтроку();

		КонецЕсли;

	Исключение

		РезультатОтправки.СообщениеОтправлено = Ложь;
		РезультатОтправки.ТекстОшибки = ОписаниеОшибки();

	КонецПопытки;

	Возврат РезультатОтправки;	
	
КонецФункции

#КонецОбласти
