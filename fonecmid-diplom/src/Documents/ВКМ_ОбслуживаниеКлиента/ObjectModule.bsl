#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ТекстСообщения = "";
	
	Если ЭтоНовый() Тогда
		
		ТекстСообщения = СтрШаблон("Создана новая заявка на обслуживание! %1 Специалист: %2 %1 Проведение работ запланировано на %3 с %4 по %5",
									Символы.ПС, Специалист, ДатаПроведенияРабот, ВремяНачалаРаботПлан, ВремяОкончанияРаботПлан);
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВКМ_ОбслуживаниеКлиента.Специалист.Представление КАК Специалист,
			|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот КАК ДатаПроведенияРабот,
			|	ВКМ_ОбслуживаниеКлиента.ВремяНачалаРаботПлан,
			|	ВКМ_ОбслуживаниеКлиента.ВремяОкончанияРаботПлан
			|ПОМЕСТИТЬ ВТ_СтарыеДанные
			|ИЗ
			|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
			|ГДЕ
			|	ВКМ_ОбслуживаниеКлиента.Ссылка = &Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	""Специалист "" + ВТ_СтарыеДанные.Специалист + "" изменён на "" + &Специалист КАК НовоеЗначение
			|ИЗ
			|	ВТ_СтарыеДанные КАК ВТ_СтарыеДанные
			|ГДЕ
			|	ВТ_СтарыеДанные.Специалист <> &Специалист
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	""Дата проведения работ "" + ПРЕДСТАВЛЕНИЕ(&ДатаПроведенияРабот) + "" (вместо "" +
			|		ПРЕДСТАВЛЕНИЕ(ВТ_СтарыеДанные.ДатаПроведенияРабот) + "")""
			|ИЗ
			|	ВТ_СтарыеДанные КАК ВТ_СтарыеДанные
			|ГДЕ
			|	ВТ_СтарыеДанные.ДатаПроведенияРабот <> &ДатаПроведенияРабот";
		
		Запрос.УстановитьПараметр("Специалист", Специалист);
		Запрос.УстановитьПараметр("ДатаПроведенияРабот", ДатаПроведенияРабот);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ТекстИзменений = "";
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ТекстИзменений = Символы.ПС + ВыборкаДетальныеЗаписи.НовоеЗначение;
			
		КонецЦикла;
		
		Если ТекстИзменений <> "" Тогда
			
			ТекстСообщения = СтрШаблон("В заявку на обслуживание клиента %1 внесены изменения! %2", Клиент, ТекстИзменений);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		
		ВКМ_Телеграм.СоздатьУведомлениеТелеграмБоту(ТекстСообщения);
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ,Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	ДоговорыКонтрагентов.ВидДоговора,
		|	ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействияДоговора КАК ДатаНачалаДействияДоговора,
		|	ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействияДоговора КАК ДатаОкончанияДействияДоговора,
		|	ДоговорыКонтрагентов.СтоимостьЧасаРаботы,
		|	ДоговорыКонтрагентов.Представление КАК ПредставлениеДоговора,
		|	ДоговорыКонтрагентов.ПометкаУдаления
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Ссылка = &Договор";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекстСообщения = "";
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Если ВыборкаДетальныеЗаписи.ВидДоговора <> Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
			
			ТекстСообщения = СтрШаблон("Вид выбранного договора %1 - %2. Необходимо выбрать договор с видом ""Абонентское обслуживание""", ВыборкаДетальныеЗаписи.ПредставлениеДоговора, ВыборкаДетальныеЗаписи.ВидДоговора);
			
		ИначеЕсли Дата < ВыборкаДетальныеЗаписи.ДатаНачалаДействияДоговора Или Дата > ВыборкаДетальныеЗаписи.ДатаОкончанияДействияДоговора Тогда
			
			ТекстСообщения = СтрШаблон("Дата документа абонентского обслуживания должна попадать в период действия договора");
		
		ИначеЕсли ВыборкаДетальныеЗаписи.ПометкаУдаления Тогда
			
			ТекстСообщения = СтрШаблон("Выбранный договор %1 помечен на удаление!");
			
		Иначе
				
			СтоимостьЧасаРаботы = ВыборкаДетальныеЗаписи.СтоимостьЧасаРаботы;	
		
		КонецЕсли;
		
	Иначе
		
		ТекстСообщения = СтрШаблон("Не удалось получить данные по договору");
		
	КонецЕсли;
		
	Если ТекстСообщения <> "" Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();
			
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СУММА(ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.ЧасыКОплатеКлиенту) КАК ЧасыКОплатеКлиенту,
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента.ВыполненныеРаботы КАК ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ОбслуживаниеКлиентаВыполненныеРаботы.Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	// регистр ВКМ_ВыполненныеКлиентуРаботы
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Клиент = Клиент;
		Движение.Договор = Договор;
		Движение.КоличествоЧасов = ВыборкаДетальныеЗаписи.ЧасыКОплатеКлиенту;
		Движение.СуммаКОплате = ВыборкаДетальныеЗаписи.ЧасыКОплатеКлиенту * СтоимостьЧасаРаботы;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти